plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

ext.versions = [
    checkerFramework: "3.49.1-eisop1",
]

dependencies {
    implementation "io.github.eisop:checker:${versions.checkerFramework}"
    implementation "io.github.eisop:checker-qual:${versions.checkerFramework}"
}

task copySources(type: Copy) {
    includeEmptyDirs = false
    doFirst {
        // Delete the directory in case a previously copied file should no longer be in checker-qual
        delete file('src/main/java')
    }

    from files('../src/main/java')
    // Replace the path below with your actual path.
    include '**/org/checkerframework/checker/linear/qual/**'

    // Make files read only.
    filePermissions {
        user {
            read = true
            write = false
        }
    }

    into file('src/main/java')
}

jar {
    dependsOn copySources
    manifest {
        attributes('Export-Package': '*')
    }
}

compileJava {
    dependsOn copySources
}

clean {
    delete file('src/')
}
